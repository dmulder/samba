#include "idl_types.h"

import "misc.idl";
import "security.idl";
import "frsrpc.idl";
import "bkupblobs.idl";
import "fscc.idl";
import "file_id.idl";
import "frstrans.idl";

/* FRS and DFS-R related blobs decoding. */

[
	pointer_default(unique),
	helpstring("dfsr and bkup blobs"),
	uuid("12345777-1234-abcd-0001-00000004")
]

interface frsblobs
{

	void  decode_nt_backup_file(
		[in] bkup_NTBackupFile file
	);

	void decode_frsrpc_StageHeader(
		[in] frsrpc_StageHeader header,
		[in] bkup_NTBackupFile data
	);

	void decode_FileNetworkOpenInformation(
		[in] fscc_FileNetworkOpenInformation data
	);

	typedef [public] struct {
		/* Just as reference, windows seems to store these fields:
		 * fid
		 * usn
		 * uidVisible
		 * filtered
		 * journalWrapped
		 * slowRecoverCheck
		 * pendingTombstone
		 * internalUpdate
		 * dirtyShutdownMismatch
		 * meetInstallUpdate
		 * meetReanimated
		 * recUpdateTime
		 * present
		 * nameConflict
		 * attributes
		 * ghostedHeader
		 * data
		 * gvsn
		 * uid
		 * parent
		 * fence
		 * clockDecrementedInDirtyShutdown
		 * clock
		 * createTime
		 * csId
		 * hash
		 * similarity
		 * name
		 */
		file_id fid;
		boolean32 meet_installed;
		frstrans_Update *update;
	} dfsr_db_record;

	typedef [public] struct {
		[in,ref,charset(DOS),string] uint8 *staged_file;
		[in,ref,charset(DOS),string] uint8 *installing_dir;
		[in,ref,charset(DOS),string] uint8 *root_dir;
		[in,ref] frstrans_Update *update;
	} dfsr_meet_install_update;

	typedef [public] struct {
		NTSTATUS result;
	} dfsr_meet_update_installed;
}
